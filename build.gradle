// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java'
    id "io.freefair.lombok" version "5.3.0"
    id 'kr.entree.spigradle' version '2.2.3'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'com.github.magnum97'
version '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// https://github.com/spigradle/spigradle#repositories
repositories {
    mavenLocal()
    paper()
    essentialsX()
    jitpack()
}

dependencies { // https://github.com/spigradle/spigradle#dependencies
    compileOnly paper()
    compileOnly essentialsX()
    implementation 'org.mineacademy:Foundation:5.8.0'
    implementation 'com.github.javafactorydev:lightningstorage:3.2.2'
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "me.magnum.project" // Default value is "shadow"

}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
tasks.prepareSpigotPlugins.dependsOn tasks.shadowJar

shadowJar {
    minimize()
//    classifier = null  // Uncomment to overwrite lean jar with shadow Jar
    version = null
}
spigot {
    authors = ['Magnum1997']
    apiVersion = '1.15'
    depends= ['GriefPrevention','WorldGuard']
    commands {
        give {
            aliases = ['i']
            description = 'Give command.'
            permission = 'test.foo'
            permissionMessage = 'You do not have permission!'
            usage = '/<command> [test|stop]'
        }
    }
    permissions {
        'buzzkill.notify.*' {
            description = 'Receive notifications when a spawn is blocked'
            defaults = 'false'
            children = ['buzzkill.notify.bee': true]
        }
        'buzzkill.notify.bee' {
            description = 'Receive notifications when a bee spawn is blocked'
            defaults = 'false'
        }
    }
}
